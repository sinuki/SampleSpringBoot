plugins {
	id 'org.springframework.boot' version '2.2.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'com.ewerk.gradle.plugins.querydsl' version '1.0.10'
	id 'java'
	id 'idea'
}

group = 'com.parksw.app'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
	compileOnly {
		extendsFrom annotationProcessor
	}
	querydsl.extendsFrom compileClasspath
}

dependencies {
	implementation(
		'org.springframework.boot:spring-boot-starter-web',
		'org.springframework.boot:spring-boot-starter-security',
		'org.springframework.boot:spring-boot-starter-thymeleaf',
		'org.springframework.boot:spring-boot-starter-data-jpa',
		'com.querydsl:querydsl-jpa',
		'com.querydsl:querydsl-apt',
	)
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor(
		'org.springframework.boot:spring-boot-configuration-processor',
		'org.projectlombok:lombok'
	)
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.security:spring-security-test'
}

test {
	useJUnitPlatform()
}

def querydslSrcDir = 'src/main/generated'

querydsl {
	library = "com.querydsl:querydsl-apt"
	jpa = true
	querydslSourcesDir = querydslSrcDir
}

compileQuerydsl.options.annotationProcessorPath = configurations.querydsl
sourceSets.main.java.srcDirs = ['src/main/java', querydslSrcDir]